Window LoginWindow {
    

    String text := ['Log In']

    Label Name_L {
        String text := ['Name']
    }
    TextField Name_TF {
        String text :=  [null] 
    }
    Label Password_L {
        String text := ['Password']
    }
    PasswordField Password_TF {
        String text :=  [null] 
    }
    
    Button Register_B {
        String text := ['Register']

        event onClick {
               open RegisterWindow()
               
        }
    }

    Button Login_B {
        String text := ['Log in']
        User loggedUser
        event onClick {
                 if [$Name_TF.text$.oclIsUndefined()] {
                    notification([''], ['No name specified '], [500])
                 }else{
                    if [$Password_TF.text$.oclIsUndefined()] {
                         notification([''], ['No password specified '], [500])
                    }else{
                        loggedUser := [User.allInstances()->any(c|c.name = $Name_TF.text$ and c.password = $Password_TF.text$)]

                        if [not($loggedUser$.oclIsUndefined())] {
                                open MainWindow(caller:[$loggedUser$], role:[$loggedUser$.role])
                        }else{
                            notification([''], ['Wrong credentials '], [500])
                        }
                    }
                 }
        }
    }
    Button Initialize_Scenario{
        //Used to delete the whole database in case of created and create a moderator just as explained in the description
        String text := ['Initialize']
        event onClick{
        // First, delete the instances of all entities
        foreach user in [User.allInstances()] {
            delete [$user$]
        }
        foreach loc in [Location.allInstances()] {
            delete [$loc$]
        }
        foreach zone in [Zone.allInstances()] {
            delete [$zone$]
        }
        foreach trip in [Trip.allInstances()] {
            delete [$trip$]
        }
        foreach rep in [Report.allInstances()] {
            delete [$rep$]
        }
        // Second, create the initial scenario
        loc_dummy := new Location
        [$loc_dummy$.location_name] := ['Mels']
        [$loc_dummy$.x] := ['9.4224']
        [$loc_dummy$.y] :=['47.0478']

         zone_dummy := new Zone
        [$zone_dummy$.zone_name] := ['Sarganserland']
        [$zone_dummy$.zone_locations] += [$loc_dummy$]

        [$loc_dummy$.location_zone] += [$zone_dummy$]
        
        
         mod := new User
        [$mod$.name] := ['admin']
        [$mod$.password] := ['secret']
        [$mod$.surname] := ['admin']
        [$mod$.role] := [Role::ADMIN]
        [$mod$.isBanned] := [false]
        [$mod$.userzone] := [$zone_dummy$]

        notification(['Message'],['The application data has been reset to the initial scenario'],[2000])
        }
        
    }
    

    
}